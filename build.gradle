plugins {
    alias(libs.plugins.benmanesVersions)
    alias(libs.plugins.detekt)
    alias(libs.plugins.kotlin)
    alias(libs.plugins.kotlinSpring)
    alias(libs.plugins.ktfmt)
    alias(libs.plugins.springBoot)
    alias(libs.plugins.springBootDependencies)
}

group = 'com.damianryan'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

kotlin {
    compilerOptions {
        progressiveMode = true
        extraWarnings = true
        freeCompilerArgs = ["-Xannotation-default-target=param-property"]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // annotation processors
    annotationProcessor libs.springBoot.configurationProcessor

    // starters
    implementation libs.springBoot.starter.webflux
    testImplementation libs.springBoot.starter.test

    // libraries
    implementation libs.jackson.module.kotlin
    implementation libs.threeten.extra
    developmentOnly libs.springBoot.devtools
}

ktfmt {
    kotlinLangStyle()
    maxWidth.set(120)
    blockIndent.set(4)
    continuationIndent.set(4)
    removeUnusedImports.set(true)
    manageTrailingCommas.set(false)
}

test {
    useJUnitPlatform()
}

bootRun.doFirst {
    def accountNumber = System.getProperty("octopus.account-number")
    def apiKey = System.getProperty("octopus.api-key")

    systemProperty "octopus.account-number", accountNumber
    systemProperty "octopus.api-key", apiKey
}